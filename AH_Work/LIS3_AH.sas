/*******************************************************************
* Client: XXXX                                                           
* Project:  COVID-19 AA                                                   
* Program: LIS3_AH.SAS  
*
* Program Type: Listing
*
* Purpose: To produce 16.2.1.3 Subject Demographics
* Usage Notes: 
*
* SAS® Version: 9.4
* Operating System:                    
*
* Author: Andrew Huang
* Date Created: 29-Mar-2023
* Modification History:
*******************************************************************/

/*TO ACCESS THE DATA*/

LIBNAME ADAM "G:\COVID08_042023\ADAM datasets";

%include "G:\COVID08_042023\PROGRAMS\_RTFSTYLE_.sas";
%_RTFSTYLE_;

DATA ADSL;
SET ADAM.ADSL;

IF BHGHTSI NE . THEN BHGHTSI_ = PUT(BHGHTSI, 8.2);
ELSE BHGHTSI_ = '';

IF BWGHTSI NE . THEN BWGHTSI_ = PUT(BWGHTSI, 8.2);
ELSE BWGHTSI_ = '';

IF BBMISI NE . THEN BBMISI_ = PUT(BBMISI, 8.2);
ELSE BBMISI_ = '';

KEEP USUBJID TRT01P AGE SEX RACE BHGHTSI_ BWGHTSI_ BBMISI_;
RUN;

/*PAGE NUMBER HANDLING*/
DATA ADSL;
SET ADSL;
RETAIN LNT 0 PAGE1 1;
LNT+1;

IF LNT>20 THEN DO;
	PAGE1=PAGE1+1;
	LNT=1;
END;
RUN;

TITLE1 J=L "COVID-19 AA";
TITLE2 J=L "Protocol: 043";
TITLE3 J=C "16.2.1.3 Subject Demographics";

FOOTNOTE1 J=L "*: AGE= TREATMENT START DATE- BIRTHDATE/365.25";
FOOTNOTE2 J=L "G:\COVID08_042023\AH_Work\LIS3_AH.SAS";

OPTIONS ORIENTATION=LANDSCAPE;
ODS ESCAPECHAR='^';
ODS RTF FILE = "G:\COVID08_042023\OUTPUTS\L_16_2_1_3.RTF" STYLE=styles.test;

PROC REPORT DATA=ADSL SPLIT="|" STYLE={OUTPUTWIDTH=100%} NOWD;
COLUMN PAGE1 USUBJID TRT01P AGE SEX RACE BHGHTSI_ BWGHTSI_ BBMISI_;

DEFINE PAGE1/ORDER NOPRINT;

DEFINE USUBJID/ORDER "Subject|Number"
STYLE (HEADER)={JUST=LEFT CELLWIDTH=12.5%}
STYLE (COLUMN)={JUST=LEFT CELLWIDTH=12.5%};

DEFINE TRT01P/ "Treatment|Sequence"
STYLE (HEADER)={JUST=LEFT CELLWIDTH=8%}
STYLE (COLUMN)={JUST=LEFT CELLWIDTH=8%};

DEFINE AGE/ "Age* (years)"
STYLE (HEADER)={JUST=LEFT CELLWIDTH=8%}
STYLE (COLUMN)={JUST=LEFT CELLWIDTH=8%};

DEFINE SEX/ "Sex"
STYLE (HEADER)={JUST=LEFT CELLWIDTH=8%}
STYLE (COLUMN)={JUST=LEFT CELLWIDTH=8%};

DEFINE RACE/ "Race"
STYLE (HEADER)={JUST=LEFT CELLWIDTH=8%}
STYLE (COLUMN)={JUST=LEFT CELLWIDTH=8%};

DEFINE BHGHTSI_/ "Height (cm)"
STYLE (HEADER)={JUST=LEFT CELLWIDTH=8%}
STYLE (COLUMN)={JUST=LEFT CELLWIDTH=8%};

DEFINE BWGHTSI_/ "Weight (kg)"
STYLE (HEADER)={JUST=LEFT CELLWIDTH=8%}
STYLE (COLUMN)={JUST=LEFT CELLWIDTH=8%};

DEFINE BBMISI_/ "BMI (kg/m2)"
STYLE (HEADER)={JUST=LEFT CELLWIDTH=8%}
STYLE (COLUMN)={JUST=LEFT CELLWIDTH=8%};

COMPUTE BEFORE _PAGE_;
LINE@1 "^{STYLE [OUTPUTWIDTH=100% BORDERTOPWIDTH=0.5PT]}";
ENDCOMP;

COMPUTE AFTER _PAGE_;
LINE@1 "^{STYLE [OUTPUTWIDTH=100% BORDERTOPWIDTH=0.5PT]}";
ENDCOMP;

BREAK AFTER PAGE1/PAGE;
RUN;

ODS _ALL_ CLOSE;
