/*******************************************************************
* Client: XXXX                                                           
* Project:  COVID-19 AA                                                   
* Program: TAB2_1_AH.SAS  
*
* Program Type: Tables
*
* Purpose: To produce Table 14.1.2  Subject Disposition by Treatment (Safety Population) 
* Usage Notes: 
*
* SAS® Version: 9.4
* Operating System:                    
*
* Author: Andrew Huang
* Date Created: 29-Mar-2023
* Modification History:
*******************************************************************/

/*TO ACCESS THE DATA*/

LIBNAME ADAM "G:\COVID08_042023\ADAM datasets";

%include "G:\COVID08_042023\PROGRAMS\_RTFSTYLE_.sas";
%_RTFSTYLE_;

/*Header part capital/Big N counts*/

DATA ADSL;
SET ADAM.ADSL;
IF SAFFL EQ 'Y';
OUTPUT;
TRT01AN=3;
TRT01A="ALL";
OUTPUT;
KEEP USUBJID TRT01A TRT01AN;
RUN;

PROC FREQ DATA=ADSL;
TABLES TRT01AN*TRT01A/OUT=CT (DROP=PERCENT RENAME=(COUNT=BIGN));
RUN;

/*CONVERTING THE COUNTS INTO MACRO VARIBALE*/

PROC SQL NOPRINT;
SELECT BIGN INTO: N1 - :N3 FROM CT;
QUIT;

%PUT &N1 &N2 &N3;

/*BODY PART SECTION COUNTS*/

DATA ADSL2;
SET ADAM.ADSL;
IF SAFFL EQ 'Y';
OUTPUT;
TRT01AN=3;
TRT01A="ALL";
OUTPUT;
KEEP USUBJID TRT01A TRT01AN RANDFL EOSSTT DCSREAS;
RUN;

/*Subjects Actual treatment */

PROC SQL NOPRINT;
CREATE TABLE SAT AS
SELECT TRT01AN, TRT01A, COUNT(DISTINCT USUBJID) AS n,
"Subjects Actual treatment" AS POP LENGTH=100,
1 AS ORD
FROM ADSL2
WHERE TRT01A NE ''
GROUP BY TRT01AN, TRT01A
ORDER BY TRT01AN, TRT01A;
QUIT;

/*Subjects Randomized */

PROC SQL NOPRINT;
CREATE TABLE RAND AS
SELECT TRT01AN, TRT01A, COUNT(DISTINCT USUBJID) AS n,
"Subjects Randomized" AS POP LENGTH=100,
2 AS ORD
FROM ADSL2
WHERE RANDFL EQ 'Y'
GROUP BY TRT01AN, TRT01A
ORDER BY TRT01AN, TRT01A;
QUIT;

/*Subjects Withdrawn */

PROC SQL NOPRINT;
CREATE TABLE WTH AS
SELECT TRT01AN, TRT01A, COUNT(DISTINCT USUBJID) AS n,
"Subjects Withdrawn" AS POP LENGTH=100,
3 AS ORD
FROM ADSL2
WHERE EOSSTT EQ 'Discontinued'
GROUP BY TRT01AN, TRT01A
ORDER BY TRT01AN, TRT01A;
QUIT;

/*Subjects Withdrawn RASONS */

PROC SQL NOPRINT;
CREATE TABLE WTHR AS
SELECT TRT01AN, TRT01A, COUNT(DISTINCT USUBJID) AS n,
DCSREAS AS POP LENGTH=100,
4 AS ORD
FROM ADSL2
WHERE EOSSTT EQ 'Discontinued'
GROUP BY TRT01AN, TRT01A, DCSREAS
ORDER BY TRT01AN, TRT01A, DCSREAS;
QUIT;

DATA FINAL;
SET SAT RAND WTH WTHR;
RUN;

PROC SORT DATA=FINAL OUT=FINAL;
BY TRT01AN;
RUN;

PROC SORT DATA=CT OUT=CT;
BY TRT01AN;
RUN;

/*MERGE n DATASET WITH Bign n DATASET*/

DATA PCT;
MERGE FINAL (IN=A) CT;
BY TRT01AN;
IF A;
GRP=PUT(n,4.)||"("||PUT(n/BIGN*100,5.1)||")";
RUN;

PROC SORT DATA = PCT OUT=PCT;
BY ORD POP;
RUN;

PROC TRANSPOSE DATA=PCT OUT=PCT1 (DROP=_NAME_);
BY ORD POP;
ID TRT01AN;
VAR GRP;
RUN;

DATA TAB2_1_FINAL;
SET PCT1;
IF _1 EQ '' THEN _1='  0';
IF _2 EQ '' THEN _2='  0';
IF _3 EQ '' THEN _3='  0';
RUN;

TITLE1 J=L "COVID-19 AA";
TITLE2 J=L "Protocol: 043";
TITLE3 J=C "Table 14.1.2  Subject Disposition by Treatment (Safety population)";

FOOTNOTE1 J=L "G:\COVID08_042023\AH_Work\TAB2_1_AH.SAS";

OPTIONS ORIENTATION=LANDSCAPE;
ODS ESCAPECHAR='^';
ODS RTF FILE ="G:\COVID08_042023\OUTPUTS\T_14_1_2.RTF" STYLE=styles.test;

PROC REPORT DATA=TAB2_1_FINAL SPLIT="|" STYLE={OUTPUTWIDTH=100%} NOWD;
COLUMN ORD POP _1 _2 _3;

DEFINE ORD/ORDER NOPRINT;

DEFINE POP/ORDER "Population"
STYLE (HEADER)={JUST=L CELLWIDTH=39%}
STYLE (COLUMN)={JUST=L CELLWIDTH=39%};

DEFINE _1/"DRUG A|(N=&N1)"
STYLE (HEADER)={JUST=L CELLWIDTH=20%}
STYLE (COLUMN)={JUST=L CELLWIDTH=20%};

DEFINE _2/"DRUG B|(N=&N2)"
STYLE (HEADER)={JUST=L CELLWIDTH=20%}
STYLE (COLUMN)={JUST=L CELLWIDTH=20%};

DEFINE _3/"DRUG ALL|(N=&N3)"
STYLE (HEADER)={JUST=L CELLWIDTH=20%}
STYLE (COLUMN)={JUST=L CELLWIDTH=20%};

COMPUTE BEFORE _PAGE_;
LINE@1 "^{STYLE [OUTPUTWIDTH=100% BORDERTOPWIDTH=0.5PT]}";
ENDCOMP;

COMPUTE AFTER _PAGE_;
LINE@1 "^{STYLE [OUTPUTWIDTH=100% BORDERTOPWIDTH=0.5PT]}";
ENDCOMP;

RUN;

ODS _ALL_ CLOSE;

