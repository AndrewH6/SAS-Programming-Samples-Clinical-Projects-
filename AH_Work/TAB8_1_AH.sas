/*******************************************************************
* Client: XXXX                                                           
* Project:  COVID-19 AA                                                   
* Program: TAB8_1_AH.SAS  
*
* Program Type: Tables
*
* Purpose: To produce Table 14.1.10 Shift Table from Baseline to End of period (Safety Population)
* Usage Notes: 
*
* SAS® Version: 9.4
* Operating System:                    
*
* Author: Andrew Huang
* Date Created: 29-Mar-2023
* Modification History:
*******************************************************************/

/*TO ACCESS THE DATA*/

LIBNAME ADAM "G:\COVID08_042023\ADAM datasets";

%include "G:\COVID08_042023\PROGRAMS\_RTFSTYLE_.sas";
%_RTFSTYLE_;

/*Header part capital/Big N counts*/

DATA ADSL;
SET ADAM.ADSL;
IF SAFFL EQ 'Y';
OUTPUT;
TRT01AN=3;
TRT01A="ALL";
OUTPUT;
KEEP USUBJID TRT01A TRT01AN;
RUN;

PROC FREQ DATA=ADSL;
TABLES TRT01AN*TRT01A/OUT=CT (DROP=PERCENT RENAME=(COUNT=BIGN));
RUN;

/*CONVERTING THE COUNTS INTO MACRO VARIBALE*/

PROC SQL NOPRINT;
SELECT BIGN INTO: N1 - :N3 FROM CT;
QUIT;

%PUT &N1 &N2 &N3;

/*BODY PART SECTION COUNTS*/

DATA ADLB;
SET ADAM.ADLB;
IF SAFFL EQ 'Y' AND PARCAT1 IN ("HEMATOLOGY" "CHEMISTRY");
TRT01AN=3;
TRT01A="ALL";

IF BNRIND ='' THEN BNRIND="MISSING";
IF ANRIND ='' THEN ANRIND="MISSING";

KEEP USUBJID PARCAT1 PARAMN PARAM AVISITN AVISIT BNRIND ANRIND TRT01AN TRT01A;
RUN;

/*Baseline to End of period */

/*                     anrind    bnrind*/
/*101 RBC V1----------NORMAL    NORMAL*/
/*101 RBC V2----------HIGH     NORMAL*/
/*101 RBC V3-----------LOW     NORMAL*/
/*101 RBC V4-----------LOW      NORMAL*/

PROC SORT DATA=ADLB;
BY USUBJID PARCAT1 PARAMN PARAM AVISITN AVISIT;
RUN;

DATA LB;
SET ADLB;
BY USUBJID PARCAT1 PARAMN PARAM AVISITN AVISIT;
IF LAST.PARAMN;
RUN;

PROC FREQ DATA=LB;
TABLES PARCAT1*PARAMN*PARAM*BNRIND*ANRIND*TRT01AN/
OUT=LB2 (DROP=PERCENT RENAME=(COUNT=n));
RUN;

PROC SORT DATA=LB2 OUT=FINAL;BY TRT01AN;RUN;
PROC SORT DATA=CT;BY TRT01AN;RUN;

/*MERGE n DATASET WITH Bign n DATASET*/

DATA PCT;
MERGE FINAL (IN=A) CT;
BY TRT01AN;
IF A;

GRP=PUT(n,4.)||"("||PUT(n/BIGN*100,5.1)||")";
RUN;

PROC SORT;BY PARCAT1 PARAMN PARAM BNRIND;RUN;

PROC TRANSPOSE DATA=PCT OUT=PCT1;
BY PARCAT1 PARAMN PARAM BNRIND;
ID ANRIND;
VAR GRP;
RUN;

DATA TAB8_1_FINAL;
SET PCT1;

IF COMPRESS(HIGH) IN ('' '0(.0)') THEN HIGH='  0';
IF COMPRESS(LOW) IN ('' '0(.0)') THEN LOW='  0';
IF COMPRESS(NORMAL) IN ('' '0(.0)') THEN NORMAL='  0';
RUN;

/*PAGE NUMBER HANDLING*/

DATA TAB8_1_FINAL;
SET TAB8_1_FINAL;
RETAIN LNT 0 PAGE1 1;
LNT+1;

IF LNT>15 THEN DO;
	PAGE1=PAGE1+1;
	LNT=1;
END;
RUN;

TITLE1 J=L "COVID-19 AA";
TITLE2 J=L "Protocol: 043";
TITLE3 J=C "Table 14.1.10 Shift Table from Baseline to End of period (Safety Population)";

FOOTNOTE1 J=L "G:\COVID08_042023\AH_Work\TAB8_1_AH.SAS";

OPTIONS ORIENTATION=LANDSCAPE;
ODS ESCAPECHAR='^';
ODS RTF FILE="G:\COVID08_042023\OUTPUTS\T_14_1_10.RTF" STYLE=styles.test;

PROC REPORT DATA=TAB8_1_FINAL SPLIT="|" STYLE={OUTPUTWIDTH=100%} NOWD;
COLUMN PAGE1 PARCAT1 PARAMN PARAM BNRIND
("Treatment  end|(N=&N3)"
"^{STYLE [OUTPUTWIDTH=100% BORDERTOPWIDTH=0.5PT]}"
LOW NORMAL HIGH);

DEFINE PAGE1/ORDER NOPRINT;
DEFINE PARCAT1/ORDER "Parameter|category"
STYLE(HEADER)={JUST=L CELLWIDTH=20%}
STYLE(COLUMN)={JUST=L CELLWIDTH=20%};

DEFINE PARAMN/ORDER NOPRINT;
DEFINE PARAM/ORDER "Parameter (Unit)"
STYLE(HEADER)={JUST=L CELLWIDTH=30%}
STYLE(COLUMN)={JUST=L CELLWIDTH=30%};

DEFINE BNRIND/ORDER "Baseline "
STYLE(HEADER)={JUST=L CELLWIDTH=19%}
STYLE(COLUMN)={JUST=L CELLWIDTH=19%};

DEFINE LOW/ "LOW "
STYLE(HEADER)={JUST=L CELLWIDTH=10%}
STYLE(COLUMN)={JUST=L CELLWIDTH=10%};

DEFINE NORMAL/ "NORMAL "
STYLE(HEADER)={JUST=L CELLWIDTH=10%}
STYLE(COLUMN)={JUST=L CELLWIDTH=10%};

DEFINE HIGH/ "HIGH "
STYLE(HEADER)={JUST=L CELLWIDTH=10%}
STYLE(COLUMN)={JUST=L CELLWIDTH=10%};

COMPUTE BEFORE _PAGE_;
LINE@1 "^{STYLE [OUTPUTWIDTH=100% BORDERTOPWIDTH=0.5PT]}";
ENDCOMP;

COMPUTE AFTER _PAGE_;
LINE@1 "^{STYLE [OUTPUTWIDTH=100% BORDERTOPWIDTH=0.5PT]}";
ENDCOMP;

COMPUTE BEFORE PARAMN;
LINE '';
ENDCOMP;

BREAK AFTER PAGE1/PAGE;
RUN;

ODS _ALL_ CLOSE;


















