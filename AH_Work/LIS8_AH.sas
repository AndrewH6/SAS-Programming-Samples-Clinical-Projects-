/*******************************************************************
* Client: XXXX                                                           
* Project:  COVID-19 AA                                                   
* Program: LIS8_AH.SAS  
*
* Program Type: Listing
*
* Purpose: To produce 16.2.2.2 Serious Adverse Events Leading to Death    
*                     16.2.2.3 Serious Adverse Events     
*                     16.2.2.4 Adverse Events
* Usage Notes: 
*
* SAS® Version: 9.4
* Operating System:                    
*
* Author: Andrew Huang
* Date Created: 29-Mar-2023
* Modification History:
*******************************************************************/

/*TO ACCESS THE DATA*/

LIBNAME ADAM "G:\COVID08_042023\ADAM datasets";

%include "G:\COVID08_042023\PROGRAMS\_RTFSTYLE_.sas";
%_RTFSTYLE_;

OPTIONS SYMBOLGEN MPRINT MLOGIC;

%MACRO AE (V1=,V2=,V3=,V4=);

DATA ADAE;
SET ADAM.ADAE;
IF &V1;
SPA=CATX("/",AETERM, AEBODSYS,AEDECOD);
KEEP USUBJID SPA AESTDTC AEENDTC AESER AACN AREL AEOUT;
RUN;

PROC SQL NOPRINT;
SELECT COUNT(*) INTO: NBR FROM ADAE;
QUIT;
%PUT &NBR;

%IF &NBR GT 0 %THEN %DO;

/*PAGE NUMBER HANDLING*/

DATA ADAE;
SET ADAE;
RETAIN LNT 0 PAGE1 1;
LNT+1;

IF LNT>7 THEN DO;
PAGE1=PAGE1+1;
LNT=1;
END;
RUN;

TITLE1 J=L "COVID-19 AA";
TITLE2 J=L "Protocol: 043";
TITLE3 J=C "&V2";

FOOTNOTE1 J=L "G:\COVID08_042023\AH_Work\&V3..SAS";

OPTIONS ORIENTATION=LANDSCAPE;
ODS ESCAPECHAR='^';
ODS RTF FILE ="G:\COVID08_042023\OUTPUTS\&V4..RTF" STYLE=styles.test;

PROC REPORT DATA=ADAE SPLIT="|" STYLE={OUTPUTWIDTH=100%} NOWD;
COLUMN PAGE1 USUBJID SPA AESTDTC AEENDTC AESER AACN AREL AEOUT;

DEFINE PAGE1/ORDER NOPRINT;
DEFINE USUBJID/ORDER "Subject|Number"
STYLE (COLUMN) ={JUST=L CELLWIDTH=12%}
STYLE (HEADER) ={JUST=L CELLWIDTH=12%};

DEFINE SPA/ORDER "Adverse Event/Primary System Organ|Class/Preffered term"
STYLE (COLUMN) ={JUST=L CELLWIDTH=15%}
STYLE (HEADER) ={JUST=L CELLWIDTH=15%};

DEFINE AESTDTC/DISPLAY "Start|Date/Time"
STYLE (COLUMN) ={JUST=L CELLWIDTH=10%}
STYLE (HEADER) ={JUST=L CELLWIDTH=10%};

DEFINE AEENDTC/DISPLAY "End |Date/Time"
STYLE (COLUMN) ={JUST=L CELLWIDTH=10%}
STYLE (HEADER) ={JUST=L CELLWIDTH=10%};

DEFINE AESER/DISPLAY "Serious|Event"
STYLE (COLUMN) ={JUST=L CELLWIDTH=6%}
STYLE (HEADER) ={JUST=L CELLWIDTH=6%};

DEFINE AACNN/ORDER NOPRINT;
DEFINE AACN/DISPLAY "Action taken"
STYLE (COLUMN) ={JUST=L CELLWIDTH=8%}
STYLE (HEADER) ={JUST=L CELLWIDTH=8%};

DEFINE ARELN/ORDER NOPRINT;
DEFINE AREL/DISPLAY "Relationship|to|Study Drug"
STYLE (COLUMN) ={JUST=L CELLWIDTH=10%}
STYLE (HEADER) ={JUST=L CELLWIDTH=10%};

DEFINE AEOUT/"Outcome"
STYLE (COLUMN) ={JUST=L CELLWIDTH=9%}
STYLE (HEADER) ={JUST=L CELLWIDTH=9%};

COMPUTE BEFORE _PAGE_;
LINE@1 "^{STYLE [OUTPUTWIDTH=100% BORDERTOPWIDTH=0.5PT]}";
ENDCOMP;

COMPUTE AFTER _PAGE_;
LINE@1 "^{STYLE [OUTPUTWIDTH=100% BORDERTOPWIDTH=0.5PT]}";
ENDCOMP;

BREAK AFTER PAGE1/PAGE;
RUN;

ODS _ALL_ CLOSE;
%END;

%IF &NBR EQ 0 %THEN %DO;

DATA ADAE;
TEXT = "NO OBSERVATIONS";
RUN;

TITLE1 J=L "COVID-19 AA";
TITLE2 J=L "Protocol: 043";
TITLE3 J=C "&V2";

FOOTNOTE1 J=L "G:\COVID08_042023\AH_Work\&V3..SAS";

OPTIONS ORIENTATION=LANDSCAPE;
ODS ESCAPECHAR='^';
ODS RTF FILE="G:\COVID08_042023\OUTPUTS\&V4..RTF" STYLE=styles.test;
PROC REPORT DATA=ADAE SPLIT="|" STYLE={OUTPUTWIDTH=100%} NOWD;
COLUMN TEXT;

DEFINE TEXT/" "
STYLE(HEADER)={JUST=C CELLWIDTH=15%}
STYLE(COLUMN)={JUST=C CELLWIDTH=15%};

COMPUTE AFTER _PAGE_;
LINE@1 "^{STYLE [OUTPUTWIDTH=100% BORDERTOPWIDTH=0.5PT]}";
ENDCOMP;
RUN;

ODS _ALL_ CLOSE;
%END;

%MEND;

%AE (V1=%STR (AESER EQ 'Y' AND AEOUT IN ("DEATH" "FATAL")),
V2=%STR (16.2.2.2 Serious Adverse Events Leading to Death),
V3=%STR (LIS8_1_AH),
V4= %STR (L_16_2_2_2)
)
;


%AE (V1=%STR (AESER EQ 'Y'),
V2=%STR (16.2.2.3 Serious Adverse Events),
V3=%STR (LIS8_2_AH),
V4= %STR (L_16_2_2_3)
)
;



%AE (V1=%STR (AETERM NE ''),
V2=%STR (16.2.2.4  Adverse Events),
V3=%STR (LIS8_3_AH),
V4= %STR (L_16_2_2_4)
)
;
