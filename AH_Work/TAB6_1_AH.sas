/*******************************************************************
* Client: XXXX                                                           
* Project:  COVID-19 AA                                                   
* Program: TAB6_1_AH.SAS  
*
* Program Type: Tables
*
* Purpose: To produce Table 14.1.8  Treatment Emergent Adverse Events by Treatment, System Organ Class and Preferred Term (safety Population)
* Usage Notes: 
*
* SAS® Version: 9.4
* Operating System:                    
*
* Author: Andrew Huang
* Date Created: 29-Mar-2023
* Modification History:
*******************************************************************/

/*TO ACCESS THE DATA*/

LIBNAME ADAM "G:\COVID08_042023\ADAM datasets";

%include "G:\COVID08_042023\PROGRAMS\_RTFSTYLE_.sas";
%_RTFSTYLE_;

/*Header part capital/Big N counts*/

DATA ADSL;
SET ADAM.ADSL;
IF SAFFL EQ 'Y';
KEEP USUBJID TRT01A TRT01AN;
RUN;

PROC FREQ DATA=ADSL;
TABLES TRT01AN*TRT01A/OUT=CT (DROP=PERCENT RENAME=(COUNT=BIGN));
RUN;

/*CONVERTING THE COUNTS INTO MACRO VARIBALE*/

PROC SQL NOPRINT;
SELECT BIGN INTO: N1 - :N2 FROM CT;
QUIT;

%PUT &N1 &N2;

/*body part*/

DATA ADAE;
SET ADAM.ADAE;
IF SAFFL EQ 'Y' AND TRTEMFL EQ 'Y';
IF AEBODSYS='' THEN AEBODSYS="UNCODED";
IF AEDECOD='' THEN AEDECOD="UNCODED";
KEEP USUBJID TRT01AN TRT01A AEBODSYS AEDECOD;
RUN;

/*Number of UNIQUE Subjects with TEAEs*/

PROC SQL NOPRINT;
CREATE TABLE ANY1 AS
SELECT TRT01AN, TRT01A, COUNT(DISTINCT USUBJID) AS N,
"Number of Subjects with TEAEs" AS AEBODSYS LENGTH=200,
1 AS ORD FROM ADAE
GROUP BY TRT01AN, TRT01A
ORDER BY TRT01AN, TRT01A;

/*BODSYS*/
CREATE TABLE BODSYS AS
SELECT TRT01AN, TRT01A, AEBODSYS, COUNT(DISTINCT USUBJID) AS N,
2 AS ORD, 2 AS X FROM ADAE
GROUP BY TRT01AN, TRT01A, AEBODSYS
ORDER BY TRT01AN, TRT01A, AEBODSYS;;

/*AEDECOD*/
CREATE TABLE DECOD AS
SELECT TRT01AN, TRT01A, AEBODSYS, AEDECOD, COUNT(DISTINCT USUBJID) AS N,
2 AS ORD, 3 AS X FROM ADAE
GROUP BY TRT01AN, TRT01A, AEBODSYS, AEDECOD
ORDER BY TRT01AN, TRT01A, AEBODSYS, AEDECOD;
QUIT;

/**/
/*PROC SORT DATA=BODSYS OUT=BODSYS_D;*/
/*BY TRT01AN TRT01A DESCENDING n;*/
/*RUN;*/

DATA FINAL;
SET ANY1 BODSYS DECOD;
RUN;

PROC SORT DATA=FINAL;BY TRT01AN;RUN;
PROC SORT DATA=CT;BY TRT01AN;RUN;

/*MERGE n DATASET WITH Bign n DATASET*/

DATA PCT;
MERGE FINAL (IN=A) CT;
BY TRT01AN;
IF A;
GRP=PUT(N,4.)||"("||PUT(N/BIGN*100,5.1)||")";
RUN;

PROC SORT;
BY ORD AEBODSYS AEDECOD;
RUN;

PROC TRANSPOSE DATA=PCT OUT=PCT1 (DROP=_NAME_);
BY ORD AEBODSYS AEDECOD;
ID TRT01AN;
VAR GRP;
RUN;

DATA PCT2;
SET PCT1;
LENGTH NEW $100.;
IF AEDECOD EQ '' THEN NEW=AEBODSYS;
ELSE NEW="    "||STRIP(AEDECOD);
RUN;

DATA TAB6_1_FINAL;
SET PCT2;
IF COMPRESS(_1) IN ('' '0(0.0)') THEN _1='  0';
IF COMPRESS(_2) IN ('' '0(0.0)') THEN _2='  0';
RUN;

/*PAGE NUMBER HANDLING*/

DATA TAB6_1_FINAL;
SET TAB6_1_FINAL;
RETAIN LNT 0 PAGE1 1;
LNT + 1;
IF LNT>15 THEN DO;
	PAGE1=PAGE1+1;
	LNT=1;
END;
RUN;

TITLE1 J=L "COVID-19 AA";
TITLE2 J=L "Protocol: 043";
TITLE3 J=C "Table 14.1.8 Treatment Emergent Adverse Events by Treatment, System Organ Class and Preferred Term (safety Population)";

FOOTNOTE1 J=L "G:\COVID08_042023\AH_Work\TAB6_1_AH.SAS";

OPTIONS ORIENTATION=LANDSCAPE;
ODS ESCAPECHAR='^';
ODS RTF FILE="G:\COVID08_042023\OUTPUTS\T_14_1_8.RTF" STYLE=styles.test;

PROC REPORT DATA=TAB6_1_FINAL SPLIT="\" STYLE={OUTPUTWIDTH=100%} NOWD MISSING;
COLUMN PAGE1 ORD AEBODSYS AEDECOD NEW ("Treatment" "^{STYLE [OUTPUTWIDTH=100% BORDERTOPWIDTH=0.5PT]}" _1 _2);

DEFINE PAGE1/ORDER NOPRINT;
DEFINE ORD/ORDER NOPRINT;
DEFINE AEBODSYS/ORDER NOPRINT;
DEFINE AEDECOD/ORDER NOPRINT;

DEFINE NEW/ORDER "MedDRA® System Organ Class|    MedDRA® Preferred Term"
STYLE(HEADER)={JUST=L CELLWIDTH=59% ASIS=ON}
STYLE(COLUMN)={JUST=L CELLWIDTH=59% ASIS=ON};

DEFINE _1/ "DRUG A|(N=&N1)"
STYLE(HEADER)={JUST=L CELLWIDTH=20%}
STYLE(COLUMN)={JUST=L CELLWIDTH=20%};

DEFINE _2/ "DRUG B|(N=&N2)"
STYLE(HEADER)={JUST=L CELLWIDTH=20%}
STYLE(COLUMN)={JUST=L CELLWIDTH=20%};

COMPUTE BEFORE _PAGE_;
LINE@1 "^{STYLE [OUTPUTWIDTH=100% BORDERTOPWIDTH=0.5PT]}";
ENDCOMP;

COMPUTE AFTER _PAGE_;
LINE@1 "^{STYLE [OUTPUTWIDTH=100% BORDERTOPWIDTH=0.5PT]}";
ENDCOMP;

COMPUTE BEFORE AEBODSYS;
LINE '';
ENDCOMP;
BREAK AFTER PAGE1/PAGE;
RUN;

ODS _ALL_ CLOSE;
















